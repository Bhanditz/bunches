apply plugin: 'application'
mainClassName = "nk.patchsets.git.general.Bunch"

buildscript {
    ext.kotlin_version = '1.2.30'
    ext.kotlin_target = '1.8'


    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

distZip {
    filesMatching("**/bin/*") { mode = 0b111101101 }
    filesMatching("**/bin/*.bat") { mode = 0b110100100 }
}

applicationDistribution.from("src/licenses") {
    into "licenses"
}

allprojects {
    group 'nk'
    version '0.0.3'

    ext.applyKotlin = {
        apply plugin: 'kotlin'

        repositories {
            mavenCentral()
        }

        dependencies {
            compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
            testCompile "junit:junit:4.12"
        }

        compileKotlin {
            kotlinOptions.jvmTarget = kotlin_target
        }

        compileTestKotlin {
            kotlinOptions.jvmTarget = kotlin_target
        }
    }
}

applyKotlin()

dependencies {
    compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '4.11.0.201803080745-r'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.2'
}
